plugins {
    id 'java-library'
    id 'maven-publish'
}

def targetJavaVersion = 17

group = "dev.upcraft.mc-library-injector"

def ENV = System.getenv()
def buildTime = ENV.BUILD_TIME ?: new Date().format('yy.MMdd.HHmm')
version = ENV.TAG ?: "${ENV.CI ? 'ci' : 'local'}-${buildTime}"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    project.group = rootProject.group
    project.version = rootProject.version

    repositories {
        mavenCentral()
        maven {
            name = 'FabricMC'
            url = 'https://maven.fabricmc.net'
        }
    }

    dependencies {
        compileOnly libs.jetbrains.annotations
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    base {
        archivesName.set(rootProject.name)
    }

    jar {
        from { rootProject.file("LICENSE.md") }
    }

    publishing {
        repositories {
            if (ENV.MAVEN_UPLOAD_URL) {
                maven {
                    url = ENV.MAVEN_UPLOAD_URL
                    credentials {
                        username = ENV.MAVEN_UPLOAD_USERNAME
                        password = ENV.MAVEN_UPLOAD_PASSWORD
                    }
                }
            }
        }
    }
}

tasks.register("copyToRoot", Copy) {
    description = 'Copies the JAR and sources JAR files to the root build directory.'
    group "build"

    ['api', 'lib'].each { subproject ->
        from { project(":${subproject}").tasks.named('jar', org.gradle.jvm.tasks.Jar) }
        from { project(":${subproject}").tasks.named('sourcesJar', org.gradle.jvm.tasks.Jar) }
        into rootProject.layout.buildDirectory.dir('libs')
    }
}

tasks.assemble.dependsOn(tasks.copyToRoot)
tasks.named('jar').configure {
    it.enabled = false
}
